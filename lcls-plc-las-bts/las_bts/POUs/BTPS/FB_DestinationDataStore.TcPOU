<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_DestinationDataStore" Id="{95d6d775-de95-4d9d-948d-6053fdff2857}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DestinationDataStore
VAR_INPUT
    {attribute 'pytmc' := '
        pv: BTPS:Name; io: input
    '}
    sName : STRING;
    (*  Do not pragma valve; Use original PVs to access valve state *)
    fbDestinationValve : REFERENCE TO FB_VGC;

    {attribute 'pytmc' := '
        pv: BTPS:ExitValveReady
        io: input
        field: ZNAM Not ready
        field: ONAM Ready
        field: ZSV MAJOR
        field: OSV NO_ALARM
    '}
    bExitValveReady : BOOL;

    {attribute 'pytmc' := '
        pv: LS
        expand: %s:BTPS
    '}
    stSourceDataStore : ARRAY [1..GVL_BTPS_Constants.nSources] OF ST_SourceDataStore;
END_VAR
VAR_OUTPUT
    //
    bDataValid : BOOL;
END_VAR
VAR
    nIndex : UINT;
    stSource: REFERENCE TO ST_SourceDataStore;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[bExitValveReady := F_IsValveReady(fbDestinationValve);

Update(
    stDataSource:=stSourceDataStore[1],
    sName:='LS1 (From laser bay 1)',
    fbSourceValve:=GVL_BTS_VAC.fb_LS1_VGC,
    fbLinear:=GVL_BTPS.fbLS1Linear,
    fbRotary:=GVL_BTPS.fbLS1Rotary,
    fbGoniometer:=GVL_BTPS.fbLS1Goniometer,
    fbNearField:=GVL_BTPS.fbLS1NearFieldCamStats,
    fbFarField:=GVL_BTPS.fbLS1FarFieldCamStats
);

Update(
    stDataSource:=stSourceDataStore[5],
    sName:='LS5 (From laser bay 3)',
    fbSourceValve:=GVL_BTS_VAC.fb_LS5_VGC,
    fbLinear:=GVL_BTPS.fbLS5Linear,
    fbRotary:=GVL_BTPS.fbLS5Rotary,
    fbGoniometer:=GVL_BTPS.fbLS5Goniometer,
    fbNearField:=GVL_BTPS.fbLS5NearFieldCamStats,
    fbFarField:=GVL_BTPS.fbLS5FarFieldCamStats
);

Update(
    stDataSource:=stSourceDataStore[8],
    sName:='LS8 (From laser bay 4)',
    fbSourceValve:=GVL_BTS_VAC.fb_LS8_VGC,
    fbLinear:=GVL_BTPS.fbLS8Linear,
    fbRotary:=GVL_BTPS.fbLS8Rotary,
    fbGoniometer:=GVL_BTPS.fbLS8Goniometer,
    fbNearField:=GVL_BTPS.fbLS8NearFieldCamStats,
    fbFarField:=GVL_BTPS.fbLS8FarFieldCamStats
);
]]></ST>
    </Implementation>
    <Method Name="Update" Id="{99a0d2c7-b05c-4070-8558-74c8b8a2c372}">
      <Declaration><![CDATA[METHOD Update : BOOL
VAR_INPUT
    sName : STRING;
END_VAR
VAR_IN_OUT
    // The data source to update
    stDataSource : ST_SourceDataStore;

    // The source information
    fbSourceValve : FB_VGC;
    fbNearField : FB_EpicsStatsMonitor;
    fbFarField : FB_EpicsStatsMonitor;
    fbLinear : FB_EpicsMotorMonitor;
    fbRotary : FB_EpicsMotorMonitor;
    fbGoniometer : FB_EpicsMotorMonitor;

END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stDataSource.sName := sName;
stDataSource.fbSourceValve REF= fbSourceValve;

stDataSource.bEntryValveReady := F_IsValveReady(fbSourceValve);

(* Update range comparisons with their source values *)
(* Cameras *)
stDataSource.fbNFCentroidX.fValue := fbNearField.fCentroidX;
stDataSource.fbNFCentroidX.bInputValid := fbNearField.bValid AND fbNearField.bIsUpdating;
stDataSource.fbNFCentroidY.fValue := fbNearField.fCentroidY;
stDataSource.fbNFCentroidY.bInputValid := fbNearField.bValid AND fbNearField.bIsUpdating;
stDataSource.fbFFCentroidX.fValue := fbFarField.fCentroidX;
stDataSource.fbFFCentroidX.bInputValid := fbFarField.bValid AND fbFarField.bIsUpdating;
stDataSource.fbFFCentroidY.fValue := fbFarField.fCentroidY;
stDataSource.fbFFCentroidY.bInputValid := fbFarField.bValid AND fbFarField.bIsUpdating;

(* Motors *)
stDataSource.fbLinear.fValue := fbLinear.fPosition;
stDataSource.fbLinear.bInputValid := fbLinear.bValid AND fbLinear.stMSTA.bHomed (* AND fbLinear.stMSTA.bClosedLoop *);
stDataSource.fbRotary.fValue := fbRotary.fPosition;
stDataSource.fbRotary.bInputValid := fbRotary.bValid AND fbRotary.stMSTA.bHomed (* AND fbRotary.stMSTA.bClosedLoop *);
stDataSource.fbGoniometer.fValue := fbGoniometer.fPosition;
stDataSource.fbGoniometer.bInputValid := fbGoniometer.bValid AND fbGoniometer.stMSTA.bHomed (* AND fbGoniometer.stMSTA.bClosedLoop *);

(* Run the range comparisons *)
stDataSource.fbNFCentroidX();
stDataSource.fbNFCentroidY();
stDataSource.fbFFCentroidX();
stDataSource.fbFFCentroidY();
stDataSource.fbLinear();
stDataSource.fbRotary();
stDataSource.fbGoniometer();

(* Data is valid overall if *all* range comparisons say so *)
stDataSource.bDataValid := (
    stDataSource.fbNFCentroidX.bInputValid AND
    stDataSource.fbNFCentroidY.bInputValid AND
    stDataSource.fbFFCentroidX.bInputValid AND
    stDataSource.fbFFCentroidY.bInputValid AND
    stDataSource.fbLinear.bInputValid AND
    stDataSource.fbRotary.bInputValid AND
    stDataSource.fbGoniometer.bInputValid AND
    TRUE
);

(* Checks only pass if data is valid and all comparisons are true *)
stDataSource.bChecksOK := (
    stDataSource.bDataValid AND
    stDataSource.fbNFCentroidX.bInRange AND
    stDataSource.fbNFCentroidY.bInRange AND
    stDataSource.fbFFCentroidX.bInRange AND
    stDataSource.fbFFCentroidY.bInRange AND
    stDataSource.fbLinear.bInRange AND
    stDataSource.fbRotary.bInRange AND
    stDataSource.fbGoniometer.bInRange AND
    stDataSource.bEntryValveReady AND
    bExitValveReady AND
    TRUE
);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>
<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_SlowVentSwitchbox" Id="{cb58497c-6e83-4528-a463-540e0b4ff42e}" SpecialFunc="None">
    <Declaration><![CDATA[(* Operation for venting the switchbox *)
FUNCTION_BLOCK FB_SlowVentSwitchbox EXTENDS FB_Operation
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR_STAT CONSTANT
	(* Expected system states before operation starts*)
	stExpPumpStatus : ST_SBPumpStatuses := (
		bRoughingPumpOn:= TRUE, 
		bTurboPumpOn:= FALSE, 
		bIonPumpOn:= TRUE);
	
	stExpValveStatus : ST_SBValveStatuses := (
		bGateValveOpen:= TRUE, 
		bVentValveOpen:= FALSE, 
		bORingPumpValveOpen:= TRUE, 
		bForelineValveOpen:= TRUE,
		bSlowPumpValveOpen:= FALSE,
		bSBVentOpen:= FALSE,  
		bSBSlowVentOpen:= FALSE);
	
	stExpGaugeReadings : ST_SBGaugeReadings := (
		eDPPiraniState:= E_PressureState.ValidLo,
		fDPColdCathodeVal:= 1E-5,
		ePumpStationPirani1State:= E_PressureState.ValidLo,
		ePumpStationPirani2State:= E_PressureState.ValidLo,
		fPumpStationColdCathodeVal:= 1E-5
	);
	stGaugeReadingsOp : ST_SBGaugeReadingsOp := (
		eDPPiraniOp:= E_GaugeCompOp.STATE, 
		eDPColdCathodeOp:= E_GaugeCompOp._LTE, 
		ePumpStationPirani1Op:= E_GaugeCompOp.STATE, 
		ePumpStationPirani2Op:= E_GaugeCompOp.STATE, 
		ePumpStationColdCathodeOp:= E_GaugeCompOp._LTE);
		
	stExpTTStatus : ST_TransportTubeStatuses := (bGateValveOpen:= FALSE);
	stTTOp : ST_TransportTubeOps := (bIonPumpIgnore:= TRUE);
		
	(* Expected system states after operation finishes *)
	stFinExpPumpStatus : ST_SBPumpStatuses := (
		bRoughingPumpOn:= FALSE, 
		bTurboPumpOn:= FALSE, 
		bIonPumpOn:= FALSE);
	
	stFinExpValveStatus : ST_SBValveStatuses := (
		bGateValveOpen:= FALSE, 
		bVentValveOpen:= FALSE, 
		bORingPumpValveOpen:= FALSE, 
		bForelineValveOpen:= FALSE, 
		bSlowPumpValveOpen:= FALSE,
		bSBVentOpen:= FALSE,  
		bSBSlowVentOpen:= FALSE);
	
	stFinExpGaugeReadings : ST_SBGaugeReadings := (
		eDPColdCathodeState := E_PressureState.Off,
		ePumpStationColdCathodeState := E_PressureState.Off
	);
	stFinGaugeReadingsOp : ST_SBGaugeReadingsOp := (
		eDPPiraniOp:= E_GaugeCompOp.ATM, 
		eDPColdCathodeOp:= E_GaugeCompOp.STATE, 
		ePumpStationPirani1Op:= E_GaugeCompOp.ATM, 
		ePumpStationPirani2Op:= E_GaugeCompOp.ATM, 
		ePumpStationColdCathodeOp:= E_GaugeCompOp.STATE);
		
	stFinExpTTStatus : ST_TransportTubeStatuses := (bGateValveOpen:= FALSE);
	stFinTTOp : ST_TransportTubeOps := (bIonPumpIgnore:= TRUE);
		
	(* Timeout constants *)
	VENT_TIMEOUT : TIME := T#10M;
	SLOW_VENT_TIMEOUT : TIME := T#30M;
	
	(* Pressure constants *)
	SLOW_VENT_PRESS : REAL := 1E-2; // Pressure (Torr) at which to begin slow venting
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[SUPER^(iq_sMessageBuffer := iq_sMessageBuffer);

CASE eStep OF
E_OpStep.IDLE: // Do nothing

E_OpStep.START: // Verify system status
	M_RunTimeoutTimer(STATUS_CHECK_TIMEOUT);
	M_SetMessage('Verifying system status');
	
	IF F_VerifySBPumpStatuses(stExpStatus := stExpPumpStatus, bEnforce := TRUE)
		AND F_VerifySBValveStatuses(stExpStatus := stExpValveStatus, bEnforce := TRUE)
		AND F_VerifySBGaugeReadings(stExpReadings := stExpGaugeReadings, stReadingsOp := stGaugeReadingsOp)
		AND F_VerifyAllTransportTubeStatuses(stExpStatus := stExpTTStatus, 
			stStatusOp := stTTOp,
			bEnforce := TRUE)
	THEN // System status check 
		M_ResetTimeoutTimer();
		M_SetMessage('Closing gate valve');
		eStep := E_OpStep.SVSB_CLOSE_GATE_VALVE;
	END_IF
	
E_OpStep.SVSB_CLOSE_GATE_VALVE:
	M_RunTimeoutTimer(VALVE_TIMEOUT);
	
	// Close gate valve to pump station to isolate pumps
	GVL_BTS_VAC.fb_PP_VGC.M_Set_OPN_SW(FALSE);
	
	IF GVL_BTS_VAC.fb_PP_VGC.M_IsClosed() THEN
		M_ResetTimeoutTimer();
		M_SetMessage('Closing foreline valve');
		eStep := E_OpStep.SVSB_CLOSE_FORELINE_VALVE;
	END_IF
	
E_OpStep.SVSB_CLOSE_FORELINE_VALVE:
	M_RunTimeoutTimer(VALVE_TIMEOUT);
	
	// Close valves to pumping station to isolate pumps
	GVL_BTS_VAC.fb_PP_VRC_2.M_Set_OPN_SW(FALSE);
	
	IF GVL_BTS_VAC.fb_PP_VRC_2.iq_stValve.eState = E_ValvePositionState.CLOSED THEN
		M_ResetTimeoutTimer();
		M_SetMessage('Turning off roughing and ion pump');
		eStep := E_OpStep.SVSB_TURN_OFF_PUMPS;
	END_IF
	
E_OpStep.SVSB_TURN_OFF_PUMPS:
	M_RunTimeoutTimer(PUMP_TIMEOUT);
	
	// Turn off pumping station pumps
	GVL_BTS_VAC.fb_PP_PMF.M_Run(FALSE);
	GVL_BTS_VAC.fb_PP_PIP.M_Run(FALSE);
	
	IF GVL_BTS_VAC.fb_PP_PMF.q_stPump.eState = E_PumpState.pumpSTOPPED AND NOT GVL_BTS_VAC.fb_PP_PIP.i_xSP_DI THEN
		M_ResetTimeoutTimer();
		M_SetMessage('Closing O-Ring valve');
		eStep := E_OpStep.SVSB_CLOSE_ORING_VALVE;
	END_IF
	
E_OpStep.SVSB_CLOSE_ORING_VALVE:
	M_RunTimeoutTimer(VALVE_TIMEOUT);
	
	// Close O-Ring pump valve
	GVL_BTS_VAC.fb_PP_VRC_1.M_Set_OPN_SW(FALSE);

	IF GVL_BTS_VAC.fb_PP_VRC_1.iq_stValve.eState = E_ValvePositionState.CLOSED THEN
		M_ResetTimeoutTimer();
		M_SetMessage('Venting');
		eStep := E_OpStep.SVSB_VENT;
	END_IF
	
E_OpStep.SVSB_VENT:
	M_RunTimeoutTimer(VENT_TIMEOUT);
		
	// Open switchbox vent
	GVL_BTS_VAC.fb_VP_VVC_1.M_Open(TRUE);
	
	// Vent until safe for slow vent
	IF GVL_BTS_VAC.fb_DP_GPI.PG.rPRESS >= SLOW_VENT_PRESS THEN
		M_ResetTimeoutTimer();
		M_SetMessage('Slow venting');
		eStep := E_OpStep.SVSB_SLOW_VENT;
	END_IF
	

E_OpStep.SVSB_SLOW_VENT:
	M_RunTimeoutTimer(SLOW_VENT_TIMEOUT);
	
	// Close main vent
	GVL_BTS_VAC.fb_VP_VVC_1.M_Open(FALSE);
	
	// Open slow vent
	GVL_BTS_VAC.fb_VP_VVC_2.M_Open(TRUE);
	
	// Vent till atmospheric pressure reached
	IF F_ATM(GVL_BTS_VAC.fb_DP_GPI.PG.rPRESS) THEN
		M_ResetTimeoutTimer();
		M_SetMessage('Closing vent');
		eStep := E_OpStep.SVSB_CLOSE_VENT;
	END_IF
	
E_OpStep.SVSB_CLOSE_VENT:
	GVL_BTS_VAC.fb_VP_VVC_2.M_Open(FALSE);
	// How to verfiy the vent is closed?
	eStep := E_OpStep.SVSB_FINISH;
	
E_OpStep.SVSB_FINISH:
	IF F_VerifySBPumpStatuses(stExpStatus := stFinExpPumpStatus, bEnforce := FALSE)
		AND F_VerifySBValveStatuses(stExpStatus := stFinExpValveStatus, bEnforce := FALSE)
		AND F_VerifySBGaugeReadings(stExpReadings := stFinExpGaugeReadings, stReadingsOp := stFinGaugeReadingsOp)
		AND F_VerifyAllTransportTubeStatuses(stExpStatus := stFinExpTTStatus, 
			stStatusOp := stFinTTOp,
			bEnforce := FALSE)
	THEN
		M_Finish();
	ELSE
		M_FatalError('End system status check failed');
	END_IF
	
ELSE
	M_FatalError('Operation is in invalid state');
END_CASE]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>